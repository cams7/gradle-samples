//export LD_LIBRARY_PATH=/home/cams7/desenv/github/gradle-samples/samples/native-binaries/prebuilt/3rd-party-lib/util/build/binaries/utilSharedLibrary/release
//export LD_LIBRARY_PATH=/home/cams7/desenv/github/gradle-samples/samples/native-binaries/prebuilt/3rd-party-lib/util/build/binaries/utilSharedLibrary/debug
buildscript {	                
	repositories {
		mavenCentral()
		maven {url uri('../../../repo')}
	}
	dependencies {	
		classpath 'dom4j:dom4j:1.6.+'	
		classpath 'br.com.cams7.plugin:platform-base:1.0-SNAPSHOT'
		classpath 'br.com.cams7.plugin:platform-native:1.0-SNAPSHOT'
		classpath 'br.com.cams7.plugin:language-native:1.0-SNAPSHOT'
	}
}

apply plugin: 'br.com.cams7.cpp'
model {
    buildTypes {
        debug
        release
    }
    repositories {
        libs(PrebuiltLibraries) {
            boost {
                headers.srcDir "3rd-party-lib/boost_1_55_0/boost"
            }
            util {
                headers.srcDir "3rd-party-lib/util/src/util/headers"
                binaries.withType(StaticLibraryBinary) {
                    def libName = targetPlatform.operatingSystem.windows ? 'util.lib' : 'libutil.a'
                    staticLibraryFile = file("3rd-party-lib/util/build/binaries/utilStaticLibrary/${buildType.name}/${libName}")
                }
                binaries.withType(SharedLibraryBinary) {
                    def os = targetPlatform.operatingSystem
                    def baseDir = "3rd-party-lib/util/build/binaries/utilSharedLibrary/${buildType.name}"
                    if (os.windows) {
                        // Windows uses a .dll file, with a different link file if it exists (not Cygwin or MinGW)
                        sharedLibraryFile = file("${baseDir}/util.dll")
                        if (file("${baseDir}/util.lib").exists()) {
                            sharedLibraryLinkFile = file("${baseDir}/util.lib")
                        }
                    } else if (os.macOsX) {
                        sharedLibraryFile = file("${baseDir}/libhello.dylib")
                    } else {
                        sharedLibraryFile = file("${baseDir}/libutil.so")
                    }
                }
            }
        }
    }

    components {
        main(NativeExecutableSpec) {
            sources {
                cpp.lib library: 'boost', linkage: 'api'
               	cpp.lib library: 'util', linkage: 'static'
               	//cpp.lib library: 'util'
            }
        }
    }
}

