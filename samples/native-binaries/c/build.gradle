//export LD_LIBRARY_PATH=/home/cams7/desenv/github/gradle-samples/samples/native-binaries/c/build/binaries/helloSharedLibrary
buildscript {	                
	repositories {
		mavenCentral()
		maven {url uri('../../../repo')}
	}
	dependencies {	
		classpath 'dom4j:dom4j:1.6.+'
		classpath 'br.com.cams7.plugin:platform-base:1.0-SNAPSHOT'
		classpath 'br.com.cams7.plugin:platform-native:1.0-SNAPSHOT'
		classpath 'br.com.cams7.plugin:language-native:1.0-SNAPSHOT'
	}
}

apply plugin: 'br.com.cams7.c'

model {
    components {
        hello(NativeLibrarySpec) {
			sources {
				c {
					source {
						srcDirs "src/hello/c"
						include "**/*.c"                        
					}
                    exportedHeaders {
						srcDirs "src/hello/include"
                    }
				}
			}
		}
		
		main(NativeExecutableSpec) {
            sources {
				c {
					source {
						srcDirs "src/main/c"
                        include "**/*.c"
					}
					
					lib library: "hello", linkage: 'static'
				}
            }			
        }		
    }
	
	binaries {
		all {
			// Define toolchain-specific compiler and linker options
			if (toolChain in Gcc) {
				cCompiler.args "-O2"
				linker.args "-Xlinker", "-S"
			}
			if (toolChain in VisualCpp) {
				cCompiler.args "/Zi"
				linker.args "/DEBUG"
			}
		}
		
		// For any shared library binaries built with Visual C++,
		// define the DLL_EXPORT macro
		withType(SharedLibraryBinarySpec) {
			if (toolChain in VisualCpp) {
				cCompiler.args "/Zi"
				cCompiler.define "DLL_EXPORT"
			}
		}
	}
}